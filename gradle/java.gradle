apply plugin: 'java'
apply plugin: 'jacoco'

def defaultEncoding = 'UTF-8'

compileJava {
    options.encoding = defaultEncoding
    options.compilerArgs = ['-Xlint:-options'] // , '-Xlint:unchecked', '-Xlint:deprecation']
}

compileTestJava {
    options.encoding = defaultEncoding
    options.compilerArgs = ['-Xlint:-options'] // , '-Xlint:unchecked', '-Xlint:deprecation']
}

configurations {
    testOutput.extendsFrom testRuntime
}

ext {
    slf4jVersion = '1.7.29'
    felixVersion = '6.0.3'
    jettyVersion = '9.4.22.v20191022'
    jacksonVersion = '2.9.10'
    jacksonDatabindVersion = '2.9.10.1'
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.5.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:2.28.2'
    testImplementation 'org.springframework:spring-test:4.1.6.RELEASE'
    testRuntimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
    testOutput sourceSets.test.output
}

task testJar( type: Jar ) {
    group 'build'
    description 'Creates a jar file with all test sources.'
    from sourceSets.test.output
    archiveClassifier.set('test')
}

task sourcesJar( type: Jar ) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = sourceCompatibility

test {
    useJUnitPlatform {
        excludeTags 'elasticsearch'
    }
    systemProperty 'java.awt.headless', 'true'
    jvmArgs '-Xmx2048m', '-noverify', '-XX:TieredStopAtLevel=1'
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    useJUnitPlatform {
        includeTags 'elasticsearch'
    }

    systemProperty 'java.awt.headless', 'true'
    jvmArgs '-Xmx2048m'

    shouldRunAfter test
}

check.dependsOn integrationTest

jacocoTestReport {
    executionData.from = files( "$buildDir/jacoco/test.exec", "$buildDir/jacoco/integrationTest.exec" )
}

jar {
    manifest {
        attributes( ['Implementation-Version': project.version] )
    }
}

publishing.publications {
    mavenJava( MavenPublication ) {
        artifact sourcesJar
    }
}

task allDependencies(type: DependencyReportTask) {}

task allDependencyInsight(type: DependencyInsightReportTask) {}
