apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'optional-base'
apply plugin: 'provided-base'

def defaultEncoding = 'UTF-8'

compileJava {
    options.encoding = defaultEncoding
    options.compilerArgs = ['-Xlint:-options'] // , '-Xlint:unchecked', '-Xlint:deprecation']
}

compileTestJava {
    options.encoding = defaultEncoding
    options.compilerArgs = ['-Xlint:-options'] // , '-Xlint:unchecked', '-Xlint:deprecation']
}

configurations {
    testOutput.extendsFrom testRuntime
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.springframework:spring-test:4.1.6.RELEASE'
    testCompile 'org.slf4j:slf4j-simple:1.7.10'
    testOutput sourceSets.test.output
}

task testJar( type: Jar ) {
    group 'build'
    description 'Creates a jar file with all test sources.'
    from sourceSets.test.output
    classifier = 'test'
}

task sourceJar( type: Jar ) {
    classifier 'sources'
    from sourceSets.main.allJava
}

artifacts {
    testOutput testJar
    archives sourceJar
}

sourceCompatibility = '1.8'
targetCompatibility = sourceCompatibility

test {
    systemProperty 'java.awt.headless', 'true'
    jvmArgs '-Xmx2048m'

    jacoco {
        enabled = ( project.properties['ci.build'] == 'true' )
        destinationFile = file( "${rootProject.buildDir}/jacoco/test.exec" )
    }
}

jar {
    manifest {
        attributes( ['Implementation-Version': project.version] )
    }
}

