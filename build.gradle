plugins {
    id 'java-base'
    id 'jacoco'
    id 'com.github.jk1.dependency-license-report'
    id "com.github.ben-manes.versions"
    id "com.enonic.defaults"
    id 'biz.aQute.bnd.builder' apply false
    id 'maven-publish'
}

ext {
    leafProjects = subprojects.findAll { p -> p.subprojects.empty }
    javaProjects = leafProjects - project( ':runtime' )
    apiProjects = rootProject.subprojects.findAll { project -> project.name.endsWith( '-api' ) }
}

allprojects {
    group = 'com.enonic.xp'

    apply plugin: 'com.enonic.defaults'
    apply from: "$rootDir/gradle/versions.gradle"
}

configure( javaProjects ) {
    apply from: "$rootDir/gradle/java.gradle"
}

configure( rootProject ) {
    apply from: "$rootDir/gradle/ci.gradle"
    apply from: "$rootDir/gradle/tools.gradle"
}

task javadoc( type: Javadoc, group: 'documentation' ) {
    title = "Enonic XP API ${version}"

    options {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        links 'https://www.javadoc.io/doc/com.google.guava/guava/26.0-jre'
        quiet()
        encoding( 'UTF-8' )
    }

    apiProjects.each { project ->
        project.tasks.withType( Javadoc ).each { javadocTask ->
            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
        }
    }
}

task javadocZip( type: Zip ) {
    from javadoc
    archiveFileName = "${project.name}-${project.version}-javadoc.zip"
    destinationDirectory = project.distsDirectory
    archiveClassifier.set( 'javadoc' )
}


publishing.publications {
    mavenJava( MavenPublication ) {
        artifact( javadocZip ) {
            artifactId = 'docs'
        }
    }
}
