buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'http://repo.enonic.net/public'
        }
    }

    dependencies {
        classpath 'com.enonic.xp:gradle-plugin:6.0.0-SNAPSHOT'
    }
}

apply plugin: 'java-base'

allprojects {
    group = 'com.enonic.xp.libs'

    repositories {
        mavenLocal()
        jcenter()
        maven { url 'http://repo.enonic.com/public' }
    }

    buildDir = 'target'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'com.enonic.xp.module'

    def defaultEncoding = 'UTF-8'

    compileJava {
        options.encoding = defaultEncoding
        options.compilerArgs = ['-Xlint:-options']
    }

    compileTestJava {
        options.encoding = defaultEncoding
    }

    task sourceJar( type: Jar ) {
        classifier 'sources'
        from sourceSets.main.allJava
    }

    artifacts {
        archives sourceJar
    }

    sourceCompatibility = '1.8'
    targetCompatibility = sourceCompatibility

    test {
        systemProperty 'java.awt.headless', 'true'
        jvmArgs '-Xmx2048m'
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository( url: 'http://repo.enonic.net/artifactory/public' )
            }
        }
    }

    dependencies {
        provided "com.enonic.xp:core-api:${version}"
        provided "com.enonic.xp:web-api:${version}"
        provided "com.enonic.xp:portal-api:${version}"
        testCompile "com.enonic.xp:testing:${version}"
    }
}
