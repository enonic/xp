plugins {
    id 'com.github.node-gradle.node'
    id 'distribution'
    id 'maven-publish'
}

node {
    download = true
    version = '14.16.1'
}

task javadocAll( type: Javadoc, group: 'documentation' ) {
    failOnError = false
    title = "Enonic XP API ${version}"
    verbose = false

    options {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        links 'https://www.javadoc.io/doc/com.google.guava/guava/26.0-jre'
        quiet()
        encoding( 'UTF-8' )
    }

    def javadocProjects = rootProject.subprojects.findAll { project -> project.name.endsWith( '-api' ) }

    javadocProjects.each { project ->
        project.tasks.withType( Javadoc ).each { javadocTask ->
            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
        }
    }
    destinationDir = file( "${buildDir}/javadoc" )
}

task javadocZip( type: Zip ) {
    from javadocAll
    archiveFileName = "${project.name}-${project.version}-javadoc.zip"
    destinationDirectory = project.distsDirectory
    archiveClassifier.set( 'javadoc' )
}

task gruntAll(type: NpmTask, dependsOn: 'npmInstall') {
    description = 'Build JSDoc for libraries.'
    args = ['run', 'jsdoc']
}

task libdocZip( type: Zip, dependsOn: 'gruntAll' ) {
    from "${buildDir}/jsdoc"
    archiveFileName = "${project.name}-${project.version}-libdoc.zip"
    destinationDirectory = project.distsDirectory
    archiveClassifier.set( 'libdoc' )
}

publishing.publications {
    mavenJava( MavenPublication ) {
        artifact javadocZip
        artifact libdocZip
    }
}
