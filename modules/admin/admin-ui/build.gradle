apply from: "$rootDir/gradle/osgi.gradle"
apply plugin: 'com.moowork.gulp'

node {
    download = true
    version = '4.2.1'
}

bundle {
    instruction 'Bundle-Name', 'Enonic XP Admin'
    instruction 'X-Bundle-Type', 'system'
}

dependencies {
    compile project( ":admin:admin-api" )
    compile project( ":script:script-api" )
    include project( ":lib:lib-auth" )
    include project( ":lib:lib-mustache" )
    include project( ":lib:lib-portal" )
}

task cleanSourceFiles( type: Delete ) {
    description = 'Clean source files that is used by Gulp.'
    delete fileTree( dir: 'src/main/resources', includes: ['**/_all.*'] )
    delete fileTree( dir: '.', includes: ['*.tmp.txt'] )
}

task gulpAll( type: GulpTask, dependsOn: npmInstall ) {
    description = 'Build admin UI resources (ts, css, etc).'
    inputs.files fileTree( dir: 'src/main/resources', exclude: '**/_all.*' )
    outputs.files fileTree( dir: 'src/main/resources', include: '**/_all.*' )
    args = ['all:clean']
}

task copyAll( type: Copy, dependsOn: gulpAll ) {
    from 'src/main/resources'
    into "$buildDir/resources/main"
}

jar.dependsOn += copyAll
clean.dependsOn += cleanSourceFiles
