apply from: "$rootDir/gradle/osgi.gradle"
apply plugin: 'com.moowork.grunt'

node {
    download = true
    version = '4.2.1'
}

bundle {
    instruction 'Bundle-Name', 'Enonic XP Admin'
}

dependencies {
    compile project( ":admin:admin-api" )
    compile project( ":script:script-api" )
    include project( ":lib:lib-auth" )
    include project( ":lib:lib-mustache" )
    include project( ":lib:lib-portal" )
}

task cleanSourceFiles( type: Delete ) {
    description = 'Clean source files that is used by Grunt.'
    delete fileTree( dir: 'src/main/resources', includes: ['**/_all.*'] )
    delete fileTree( dir: '.', includes: ['*.tmp.txt'] )
}

task gruntAll( type: GruntTask, dependsOn: npmInstall ) {
    description = 'Build admin UI resources (ts, css, etc).'
    inputs.files fileTree( dir: 'src/main/resources', exclude: '**/_all.*' )
    outputs.files fileTree( dir: 'src/main/resources', include: '**/_all.*' )
    args = ['all']
}

task copyAll( type: Copy, dependsOn: gruntAll ) {
    from 'src/main/resources'
    into "$buildDir/resources/main"
}

jar.dependsOn += copyAll

clean.dependsOn += cleanSourceFiles
