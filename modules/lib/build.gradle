plugins {
    id 'base'
    id 'com.github.node-gradle.node'
}

node {
    download = true
    version = '16.15.0'
}

/* Test */

task lint( type: NpmTask, dependsOn: npmInstall ) {
    description = 'Lint TS, JS, and JSON'
    args = [ 'run', 'lint:quiet' ]

    outputs.upToDateWhen { false }
}

check.dependsOn lint

/* Configure & Build */

task typescript( type: NpmTask, dependsOn: npmInstall ) {
    description = 'Create JS and DTS files from TS'
    args = [ 'run', 'build' ]

    inputs.files( subprojects.buildDir.collect { fileTree("$it/typescript/lib/xp/").matching {
        include "**/*.ts"
    }} )
    inputs.file "global.d.ts"

    inputs.file "tsconfig.build.json"
    inputs.file "tsconfig.json"

    outputs.files( subprojects.buildDir.collect { fileTree("$it/typescript/lib/xp/").matching {
        include "**/*.js"
    }} )
}

task prepareToJsdoc( type: Copy, dependsOn: typescript ) {
    into "$buildDir/js"
    into("") {
        from projectDir
        include "lib-*/build/typescript/lib/xp/*.js"
        eachFile { fcd ->
            fcd.relativePath = new RelativePath(true, fcd.relativePath.segments.drop(3))
        }

    }
    into ("examples") {
        from projectDir
        include "lib-*/src/main/resources/lib/xp/examples/**/*.js"
        eachFile { fcd ->
            fcd.relativePath = new RelativePath(true, fcd.relativePath.segments.drop(5))
        }
    }
    includeEmptyDirs = false
}

task jsdoc(type: NpmTask, dependsOn: prepareToJsdoc) {
    description = 'Build JSDoc for libraries.'
    args = ['run', 'jsdoc']
    doFirst {
        copy {
            from 'jsdoc/template/jsdoc-main.css'
            into "$buildDir/resources/main/jsdoc/styles"
        }
    }
}

/* Publish */

task prepareToPublish( type: Copy ) {
    into "$buildDir/npm"

    from 'README.md'
    from 'global.d.ts'

    from( 'package.template.json' ) {
        filter {
            line -> line
                .replaceAll('%VERSION%', version )
                .replaceAll('%FULL_NAME%|%FILE_NAME%', 'global' )
                .replaceAll('%SHORT_NAME%', 'common' )
        }
        rename '.+', 'package.json'
    }
}

task publishToNpm( type: NpmTask, dependsOn: [ prepareToPublish, npmInstall ] ) {
    onlyIf { !version.endsWith('-SNAPSHOT') }

    args = [ 'publish' ]
    workingDir = prepareToPublish.destinationDir
}

task packageJsdoc(type: Jar, dependsOn: jsdoc) {
    from "$buildDir/resources/main/jsdoc"
    classifier = 'jsdoc'
}

task publish {
    dependsOn publishToNpm
}

artifacts {
    archives packageJsdoc
}

publishing.publications {
    mavenJava( MavenPublication ) {
        artifact( packageJsdoc ) {
            artifactId = 'docs'
        }
    }
}

subprojects {
    apply plugin: 'com.github.node-gradle.node'

    task prepareToTypescript( type: Copy ) {
        from ("src/main/resources") {
            include '**/*.ts'
            includeEmptyDirs = false
        }
        into "$buildDir/typescript"
    }

    parent.typescript.dependsOn prepareToTypescript

    processResources {
        from( "$buildDir/typescript" ) {
            include '**/*.js'
        }
        dependsOn parent.typescript
    }

    task prepareToPublish( type: Copy, dependsOn: processResources ) {
        def fullName = project.name
        def shortName = project.name.substring( 4 )

        into "$buildDir/npm"

        from( 'README.md' )
        from( "$buildDir/typescript/lib/xp/" ) {
            include '*.d.ts'
        }

        from( '../package.template.json' ) {
            filter {
                line -> line
                    .replaceAll('%VERSION%', version )
                    .replaceAll('%FULL_NAME%', fullName )
                    .replaceAll('%SHORT_NAME%|%FILE_NAME%', shortName )
            }
            rename '.+', 'package.json'
        }
    }

    task publishToNpm( type: NpmTask, dependsOn: prepareToPublish ) {
        onlyIf { !version.endsWith('-SNAPSHOT') }
        args = [ 'publish' ]
        workingDir = prepareToPublish.destinationDir
    }

    publish.dependsOn publishToNpm

    check.dependsOn parent.lint

    jar {
        exclude '**/*.ts'
    }
}
