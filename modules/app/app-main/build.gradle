apply from: "$rootDir/gradle/osgi.gradle"
apply plugin: 'com.moowork.gulp'


dependencies {
    compile project( ":admin:admin-ui" )
    include project( ":lib:lib-mustache" )
    include project( ":lib:lib-portal" )
    include project( ":lib:lib-admin-ui" )
    include project( ":lib:lib-auth" )
}

node {
    download = true
    version = '6.9.2'
}

ext {
    resourcesDir = file( 'src/main/resources' )
    buildResourcesDir = file( "${buildDir}/resources/main" )
}

bundle {
    instruction 'Bundle-Name', 'Admin Main App'
    instruction 'X-Bundle-Type', 'system'
    instruction 'X-Source-Paths', "$resourcesDir, $buildResourcesDir"
}

task gulpAll( type: GulpTask, dependsOn: npmInstall ) {
    description = 'Build UI resources (css, etc).'
    inputs.dir 'src/main/resources'
    outputs.dir 'build/resources/main'
    args = ['all']
}

jar.dependsOn += gulpAll
jar.outputs.dir "${buildDir}/resources/main"

task cleanNode( type: Delete ) {
    delete 'node_modules'
}
