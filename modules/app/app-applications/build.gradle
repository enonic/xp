apply from: "$rootDir/gradle/osgi.gradle"
apply plugin: 'com.moowork.node'


dependencies {
    compile project( ":admin:admin-ui" )
    include project( ":lib:lib-mustache" )
    include project( ":lib:lib-portal" )
    include project( ":lib:lib-admin" )
}

node {
    download = true
    version = '6.9.2'
}

ext {
    resourcesDir = file( 'src/main/resources' )
    buildResourcesDir = file( "${buildDir}/resources/main" )
}

bundle {
    instruction 'Bundle-Name', 'Admin Applications App'
    instruction 'X-Bundle-Type', 'system'
    instruction 'X-Source-Paths', "$resourcesDir, $buildResourcesDir"
}

task webpack( type: NodeTask, dependsOn: npmInstall ) {
    description = 'Build UI resources (ts, css, etc).'
    script = file( './node_modules/webpack/bin/webpack' )
    args = ['--config', "$rootDir/scripts/util/webpack.config.js"]
}

task cleanNode( type: Delete ) {
    delete 'node_modules'
}

jar.dependsOn += webpack
jar.outputs.dir "${buildDir}/resources/main"
